include fg from ansi


static function min(array) {
    let minimum = array.0

    for item in array 
        if item < minimum 
            minimum = item

    return minimum
}

static function max(array) {
    let maximum = array.0

    for item in array 
        if item > maximum
            maximum = item

    return maximum
}

static function padstr(string, length, char = " ") {
    string = String(string)

    if string.length >= length
        return string

    return (char * (length - string.length)) + string
}

function format(source_lines, message, error_type, filename, location, markers, color=fg(255, 30, 10)) {
    let min_line = min(markers.map(function(item) item.line))
    let max_line = max(markers.map(function(item) item.line))
    let max_line_number_length = String(max_line).length + 1
    let formatted = ""

    formatted += $"{' ' * max_line_number_length}──> {color}{error_type} in {filename}:{location.line+1}:{location.column+1}{fg.rs}\n"

    for line in range(min_line, max_line + 1) {
        let matching_markers = markers.filter(function(marker) marker.line == line)

        if matching_markers.length == 0
            continue
        
        formatted += $"{' ' * max_line_number_length} │\n"
        formatted += $"{padstr(line + 1, max_line_number_length)} │ {source_lines[line]}\n"
        
        for offset in range(0, matching_markers.length + 1) {
            if offset == 0 {
                let out = ""

                for marker in matching_markers {
                    out += " " * (marker.column - out.length)
                    out += "─" * (marker.length - 1)
                    out += "┬"
                }

                formatted += $"{' ' * max_line_number_length} │ {out}\n"
                continue
            }

            let current_marker = matching_markers.length - offset
            let out = ""

            for marker_index in range(0, matching_markers.length) {
                let marker = matching_markers.(marker_index)

                out += " " * (marker.column - out.length)
                out += " " * (marker.length - 1)

                if marker_index > current_marker {
                    out += " "
                } else if current_marker == marker_index {
                    out += "╰── " + marker.message
                } else {
                    out += "│"
                }
            }

            formatted += $"{' ' * max_line_number_length} . {out}\n"
        }
    }

    formatted += $"{' ' * max_line_number_length} │\n"
    formatted += $"{' ' * max_line_number_length}──> {color}{message}{fg.rs}\n"

    return formatted
}
if is_main {
    include Highlight from SyntaxHighlighter
    let code = Highlight("\n\n\n\n\n\n\n\n\n\nlet my_number = 1234\nlet my_string = 'string'\nstdout.write(my_number + my_string, '\\n')", {linenums: false})
    stderr.write(format(code.split("\n"), "+ is undefined for types `Number` and `String`", "TypeError", "<main>", {
        line: 2,
        column: 16
    }, [
        {
            line: 10,
            column: 4,
            length: 9,
            message: "left side declared here"
        },
        {
            line: 10,
            column: 16,
            length: 4,
            message: "Number"
        },
        {
            line: 11,
            column: 4,
            length: 9,
            message: "right side declared here"
        },
        {
            line: 11,
            column: 16,
            length: 8,
            message: "String"
        },
        {
            line: 12,
            column: 13,
            length: 9,
            message: "Type `Number`"
        },
        {
            line: 12,
            column: 23,
            length: 1,
            message: "Addition"
        },
        {
            line: 12,
            column: 25,
            length: 9,
            message: "Type `String`"
        }
    ]))
    exit(1)
}